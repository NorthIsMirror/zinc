# vim: ft=zsh ts=2 sw=2 et fenc=utf-8

typeset -gA _P10K_SEP_CHARS

_P10K_SEP_CHARS[left]=${_P10K_SEP_CHARS[left]:-''}
_P10K_SEP_CHARS[right]=${_P10K_SEP_CHARS[right]:-''}
_P10K_SEP_CHARS[left_bgmatch]=${_P10K_SEP_CHARS[left_bgmatch]:-''}
_P10K_SEP_CHARS[right_bgmatch]=${_P10K_SEP_CHARS[right_bgmatch]:-''}
if (( ! ${+_P10K_SEP_CHARS[rprompt_terminating_space]} )); then
  # if in tmux/etc don't use the hidden end space trick
  # causes alignment issues with the left prompt -> command
  if [[ "${TMUX}${SCREEN_NO}" != "" ]]; then
    _P10K_SEP_CHARS[rprompt_terminating_space]=" %{%k%f%}"
  else
    _P10K_SEP_CHARS[rprompt_terminating_space]="%{ %k%f%}"
  fi
fi

function p10k_render_prompt_from_spec () {
  _P10K_DBG_OUT "\n>>> build_prompt: $1 $2"
  # Builds the prompt with the given name.
  #
  # $1: Name of a p10k prompt segment array. Required.
  # $2: Name of a p10k segment options array.
  # $3: Render direction
  # $4: Chars to use as segment separator.

  [[ "${(P)#1}" == "0" ]] && _P10K_DBG_OUT 'ERR: no segments' && return
  _P10K_DBG_OUT "${(P)#1} segments to render"

  local _P10K_SBUF=""

  # HACK
  # _P10K_DBG_OUT "_P10K_ASYNC_STATII: ${(kv)_P10K_ASYNC_STATII}"

  [[ ${3} != "right" ]] && 3="left"

  local prev_bg
  [[ $3 != "right" ]] && prev_bg="NOCONNECT"
  local -A _p10k_opts
  _p10k_opts=(${(kvP)${2}})
  local -a segment_opts
  local segment_optstr

  for cur_segment in ${(P)${1}}; do

    # Autoload function
    _p10k_tracked_autoload $cur_segment

    # Get segment options, fill needed defaults
    segment_optstr="${_p10k_opts[$cur_segment]}"
    # _P10K_DBG_OUT "$cur_segment segment_optstr: ${segment_optstr}"
    segment_opts=("${(@s.;.)segment_optstr}")
    # _P10K_DBG_OUT "$cur_segment opts before default application: type(${(t)segment_opts}) ${segment_opts}"
    local segment_opts_default_filled=0
    for segment_opt_number in {1..4}; do
      [[ "${segment_opts[$segment_opt_number]}" == "" ]] && {
        # then we need to get the default opt here
        [[ "${_P10K_SEGMENT_DEFAULTS[${cur_segment}_${segment_opt_number}]}" == "" ]] && {
          # _P10K_DBG_OUT "getting default opts for $cur_segment ${segment_opt_number}"
          _p10k_tracked_autoload "${cur_segment}_default_opts"
          local -a reply
          reply=(NONE NONE normal normal)
          ${cur_segment}_default_opts
          # _P10K_DBG_OUT "$cur_segment default opts: ${(j.,.)reply}"
          for v in {1..4}; do
            _P10K_SEGMENT_DEFAULTS[${cur_segment}_${v}]=${reply[${v}]}
          done
        }
        # set the value from _P10K_SEGMENT_DEFAULTS cache
        segment_opts[${segment_opt_number}]="${_P10K_SEGMENT_DEFAULTS[${cur_segment}_${segment_opt_number}]}"
        segment_opts_default_filled=1
      }
    done
    (( segment_opts_default_filled == 1 )) && {
      _P10K_DBG_OUT "filling options assoc ${2}[${cur_segment}] with new defaults"
      eval "${2}[${cur_segment}]=\${(j.;.)segment_opts}"
    }
    # _P10K_DBG_OUT "opts for "$cur_segment':' "${(j.,.)segment_opts}"

    # Detect segments with asynchronous output enabled
    # async status "" means not yet started
    [[ "${segment_opts[4]}" == "async" ]] && \
    [[ "${_P10K_ASYNC_STATII[${cur_segment}_async]}" == "" ]] && {
      # _p10k_tracked_autoload "${cur_segment}_async"
      _P10K_DBG_OUT "asyncing segment ${cur_segment}"
      async_worker_eval p10k_segment_worker "_p10k_tracked_autoload" "${cur_segment}_async"
      [[ "${(t)_P10K_ASYNC_STATII}" == "association" ]] || typeset -gA _P10K_ASYNC_STATII
      _p10k_tracked_autoload "${cur_segment}_async_started"
      ${cur_segment}_async_started "${(@)segment_opts}"
      _P10K_ASYNC_STATII[${cur_segment}_async]="started"
      _P10K_DBG_OUT "async_job: ${cur_segment}_async !! ${(@)segment_opts}"
      _P10K_DBG_OUT "_P10K_ASYNC_STATII now: ${(kv)_P10K_ASYNC_STATII}"
      async_job p10k_segment_worker "${cur_segment}_async" "${(@)segment_opts}"
    }

    # Run the segment and gather output:
    REPLY=""
    ${cur_segment} "${(@)segment_opts}"
    local segment_output="$REPLY"

    # Detect segments with conditional display
    if [[ "${segment_opts[3]}" == "CONDITIONAL" ]]; then
      _p10k_tracked_autoload "${cur_segment}_display_hidden"
      ${cur_segment}_display_hidden && {
        _P10K_DBG_OUT "not displaying ${cur_segment}"
        continue
      }
    fi

    # Detect segments with conditional colors
    if [[ "${segment_opts[1]}" == "CONDITIONAL" ]]; then
      _p10k_tracked_autoload "${cur_segment}_fg"
      REPLY="white"
      ${cur_segment}_fg
      segment_opts[1]="${REPLY}"
    fi
    if [[ ${segment_opts[2]} == "CONDITIONAL" ]]; then
      _p10k_tracked_autoload "${cur_segment}_bg"
      REPLY="black"
      ${cur_segment}_bg
      segment_opts[2]="${REPLY}"
    fi

    _P10K_DBG_OUT "prev_bg: ${prev_bg}, current_bg: ${segment_opts[2]}"

    # Detect segment connection
    if [[ $prev_bg == "NOCONNECT" ]] || [[ ${segment_opts[3]} == "CONNECT_PREV" ]]; then
      # skip drawing the arrow
      _P10K_SBUF+="%{%K{${segment_opts[2]}}%} "
    elif [[ "${prev_bg}" == "${segment_opts[2]}" ]]; then
      _P10K_DBG_OUT "joining segments with same bg"
      [[ "$3" == "left" ]] && {
        _P10K_SBUF+=" %{%K{${segment_opts[2]}}%F{${prev_bg}}%}${_P10K_SEP_CHARS[${3}_bgmatch]} "
      } || {
        _P10K_SBUF+=" %{%F{${segment_opts[1]}}%}${_P10K_SEP_CHARS[${3}_bgmatch]}%{%K{${segment_opts[2]}}%} "
      }
    else
      [[ "$3" == "left" ]] && {
        _P10K_SBUF+=" %{%K{${segment_opts[2]}}%F{${prev_bg}}%}${_P10K_SEP_CHARS[${3}]} "
      } || {
        _P10K_SBUF+=" %{%F{${segment_opts[2]}}%}${_P10K_SEP_CHARS[${3}]}%{%K{${segment_opts[2]}}%} "
      }
    fi

    # print actual segment content
    _P10K_SBUF+="%{%F{${segment_opts[1]}}%}"
    _P10K_SBUF+="${segment_output}"

    # store for next loop
    prev_bg="${segment_opts[2]}"
  done

  # finish the prompt line:
  # the `%{ %}` is an invis wrapper
  [[ "$3" == "left" ]] && {
    [[ "${segment_opts[3]}" != "ENDLINE" ]] && {
      _P10K_SBUF+=' %{%k%F{'${prev_bg}'}%}'${_P10K_SEP_CHARS[${3}]}'%f '
    }
  } || { # right prompt
    _P10K_SBUF+="${_P10K_SEP_CHARS[rprompt_terminating_space]}"
  }

  REPLY="$_P10K_SBUF"

  _P10K_DBG_OUT "<<< render done."
}

_P10K_AUTOLOADED_FUNCTIONS+=("p10k_render_prompt_from_spec")

p10k_render_prompt_from_spec "$@"
