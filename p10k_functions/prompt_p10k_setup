# vim: ft=zsh ts=2 sw=2 et fenc=utf-8
#
# P10K
#
# A powerline ZSH theme, inspired by Powerlevel9k and friends.

autoload -Uz async && async

_P10K_DBG_OUT () {
  # ' >/tmp/_P10K_DBG_OUT.log 2>&1'
  echo -e "$@" >>/tmp/_P10K_DBG_OUT.log
}

async_start_worker p10k_segment_worker -n

typeset -gA _P10K_ASYNC_STATII

function p10k_segment_async_callback () {
  # Async args:
  # $1 job name, e.g. the function passed to async_job
  # $2 return code
  # $3 resulting (stdout) output from job execution
  # $4 execution time, floating point e.g. 0.0076138973 seconds
  # $5 resulting (stderr) error output from job execution
  # $6 has next result in buffer (0 = buffer empty, 1 = yes)

  # _P10K_DBG_OUT "p10k_segment_async_callback \"$1\" RET:$2"

  # ignore the async evals used to alter worker environment
  [[ "${1}" == "[async/eval]" ]] && return

  (( ${+functions[${1}_return]} )) || {
    autoload -U +X "${1}_return"
    _P10K_AUTOLOADED_FUNCTIONS+=("${1}_return")
  }
  "${1}_return" "$2" "$3" "$4" "$5"
  [[ "${(t)_P10K_ASYNC_STATII}" == "association" ]] || typeset -A _P10K_ASYNC_STATII
  _P10K_ASYNC_STATII[${1}]="returned"
  _P10K_DBG_OUT "_P10K_ASYNC_STATII now: ${(kv)_P10K_ASYNC_STATII}"

  # TODO if $6 == 0 then redraw prompt with new information
  [[ "$6" == "0" ]] && prompt_p10k_render_to_vars && zle .reset-prompt

  _P10K_ASYNC_STATII[${1}]="finished"
}

async_register_callback p10k_segment_worker p10k_segment_async_callback

typeset -aU _P10K_AUTOLOADED_FUNCTIONS
function p10k_reload () {
  async_flush_jobs p10k_segment_worker
  for segment_function in $_P10K_AUTOLOADED_FUNCTIONS; do
    unfunction $segment_function
    echo "Reloading ${segment_function}"
    builtin autoload -U +X $segment_function
  done
  builtin unfunction prompt_p10k_setup
  builtin autoload -U +X prompt_p10k_setup
}

prompt_p10k_setup () {
  prompt_opts=( cr percent sp subst )
  setopt noprompt{cr,percent,sp,subst} "prompt${^prompt_opts[@]}"

  builtin autoload -Uz add-zsh-hook
  builtin autoload -U +X p10k_render_prompt_from_spec

  # association
  typeset -gA p10k_opts _P10K_SEGMENT_DEFAULTS _P10K_ASYNC_STATII
  # array
  typeset -ga p10k_left p10k_right

  # Setup precmd / preexec
  _P10K_ASYNC_STATII["test"]="confirm"
  add-zsh-hook precmd prompt_p10k_precmd
  add-zsh-hook preexec prompt_p10k_preexec

  PROMPT="%f%b%k\${_P10K_RENDERED_OUTPUT_PROMPT} "
  RPROMPT="%f%b%k\${_P10K_RENDERED_OUTPUT_RPROMPT}%f%b%k"
}

prompt_p10k_setup "$@"
