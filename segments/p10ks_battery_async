# vim: ft=zsh ts=2 sw=2 et fenc=utf-8

if [[ "$(uname -s )" == "Linux" ]]; then
  function p10ks_battery_async () {
    # The battery can have four different states - default to 'unknown'.
    local current_state='unknown'
    local sysp="${ROOT_PREFIX}/sys/class/power_supply"
    local potential_bats=( "$sysp/*" )
    [[ ${#${(M)potential_bats:#*(BAT|battery)*}} ]] \
      && local bats=(${$(ls -d $sysp/(battery|BAT*))}) \
      || return

    local numerator="0"
    local denominator="0"
    local battery_status_full=true
    local battery_status_charging=false
    for bat in $bats; do
      numerator+="+ $(cat $bat/capacity) * $(cat $bat/(energy|charge)_full)"
      denominator+="+ $(cat $bat/(energy|charge)_full)"
      [[ $(cat $bat/status) != Full ]] && battery_status_full=false
      [[ $(cat $bat/status) == Charging ]] && battery_status_charging=true
    done

    local capacity=$(( ($numerator)/($denominator) ))
    [[ $capacity -gt 100 ]] \
      && local bat_percent=100 \
      || local bat_percent=$capacity
    [[ $battery_status_full == true || $battery_status_charging == true ]] \
      && local connected=true

    if [[ -z $connected ]]; then
      [[ $bat_percent -lt $P9K_BATTERY_LOW_THRESHOLD ]] \
        && current_state="low" \
        || current_state="disconnected"
    else
      [[ $bat_percent =~ 100 ]] && current_state="charged"
      [[ $bat_percent -lt 100 ]] && current_state="charging"
    fi
    if [[ -f ${ROOT_PREFIX}/usr/bin/acpi ]]; then
      declare -a acpi_lines
      acpi_lines=( "${(@f)$(${ROOT_PREFIX}/usr/bin/acpi)}" )
      local time_remaining=${${=${(M)acpi_lines:#*([[:digit:]][[:digit:]]:|rate)*}}[5]}
      unset acpi_lines

      if [[ $time_remaining =~ "rate" ]]; then
        local tstring="..."
      elif [[ $time_remaining =~ "[[:digit:]]+" ]]; then
        local tstring=${(f)$(date -u -d "$(echo $time_remaining)" +%k:%M 2> /dev/null)}
      fi
    fi
    # [[ -n $tstring ]] && local remain=" ($tstring)"

    builtin print "${current_state};${bat_percent};${tstring}"
  }
else
  function p10ks_battery_async () {
    # no support for OS
  }
fi

function p10ks_battery_async_old () {
  # TODO more configurations
  local ROOT_PREFIX="${4}"

  # return if P9K_BATTERY_HIDE_ABOVE_THRESHOLD is set and the battery percentage is greater or equal
  if p9k::defined P9K_BATTERY_HIDE_ABOVE_THRESHOLD && [[ "${bat_percent}" -ge $P9K_BATTERY_HIDE_ABOVE_THRESHOLD ]]; then
    return
  fi

  local message
  if [[ "$P9K_BATTERY_VERBOSE" == true ]]; then
    message="$bat_percent%%$remain"
  else
    message="$bat_percent%%"
  fi

  # override default icon if we are using battery stages
  local overideIcon="" overideBg="" segment
  declare -i offset
  if [[ -n "$P9K_BATTERY_STAGES" ]] && [[ -n "$P9K_BATTERY_LEVEL_BACKGROUND" ]]; then
    # override default icon if we are using battery stages
    if [[ -n "$P9K_BATTERY_STAGES" ]]; then
      segment=$(( 100.0 / (${#P9K_BATTERY_STAGES} - 1 ) ))
      if [[ ${segment} > 1 ]]; then
        offset=$(( (${bat_percent} / $segment) ))
        # check if the stages are in an array or a string
        [[ "${(t)P9K_BATTERY_STAGES}" =~ "array" ]] && overideIcon="$P9K_BATTERY_STAGES[$offset]" || overideIcon="${P9K_BATTERY_STAGES:$offset:1}"
      fi
    fi

    # override the default color if we are using a color level array
    if [[ -n "$P9K_BATTERY_LEVEL_BACKGROUND" ]] && [[ "${(t)P9K_BATTERY_LEVEL_BACKGROUND}" =~ "array" ]]; then
      segment=$(( 100.0 / (${#P9K_BATTERY_LEVEL_BACKGROUND} - 1 ) ))
      offset=$(( (${bat_percent} / $segment) + 1 ))
      overideBg="$(p9k::background_color ${P9K_BATTERY_LEVEL_BACKGROUND[$offset]})"
    fi
  fi
  unset offset
  # Draw the prompt_segment
  p9k::prepare_segment "$0" "${(U)current_state}" $1 "$2" $3 "${message}" "" "${overideIcon}" "${overideBg}"
}

p10ks_battery_async "$@"
